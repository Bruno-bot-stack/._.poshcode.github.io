
<!doctype html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
    <meta name="description" content="PoshCode - Community resources for PowerShell coders">
    <meta name="author" content="vocatus">
    <title>DHCP Failover Script - PoshCode</title>

    <link rel="stylesheet" href="/css/bootstrap.min.css">
    <link rel="stylesheet" href="/css/main.css">
    <style>
        body {
            padding-top: 50px;
            padding-bottom: 20px;
        }
    </style>
</head>
<body>
    <header>
    <nav class="navbar navbar-expand-sm fixed-top navbar-dark bg-dark">
        <div class="container">
            <div class="navbar-header">
                <a class="navbar-brand" href="http://PoshCode.org/">PoshCode</a>
                <button class="navbar-toggler" type="button" data-toggle="collapse" data-target="#navbarResponsive" aria-controls="navbarResponsive" aria-expanded="false" aria-label="Toggle navigation">
                    <span class="navbar-toggler-icon"></span>
                </button>
            </div>
            <div id="navbarResponsive" class="collapse navbar-collapse navbar-responsive-collapse navbar-right">
                <ul class="nav navbar-nav nav-tabs ml-auto" data-tabs="tabs" id="tabs">
                    <li class="nav-item"><a class="nav-link active show" href="/scripts" data-toggle="tab">Scripts</a></li>
                    <li class="nav-item"><a class="nav-link" href="/video" data-toggle="tab">Video Feed</a></li>
                </ul>
            </div><!--/.navbar-collapse -->
        </div>
    </nav>
    </header>

    

<div class="blog-post">
    <h2 class="blog-post-title">DHCP Failover Script</h2>
    <p class="blog-post-meta">
        <span class="blog-post-time">2012-08-09</span> by <a class="blog-post-author">vocatus</a>
    </p>

    <h1>DHCP Failover Script</h1>
<h3><a href="//scripts/3562.ps1">download</a></h3>
<p>DHCP Failover/Watchdog script.
This script runs on a backup/alternate DHCP server as a scheduled task and monitors a primary server. Every 10 minutes or so (configurable) it checks the primary server, and if it cannot be reached, fires up its own DHCP server with the most recently fetched copy of the primary server's database. It will continue to monitor the primary server until it sees it come back up, at which point it will deactivate it's own DHCP server and return to watchdog mode.</p>
<p>This is a legacy shell script (batch), rather than PowerShell.</p>
<p>Read all comments and set variables as you desire.</p>
<p>NOTE: I haven't implemented &quot;fail-back&quot; capability yet.</p>
<pre><code class="language-posh">:: Purpose:         DHCP server Watchdog &amp; Failover script. Read notes below
:: Requirements:    1. Domain administrator credentials &amp; &quot;Logon as a batch job&quot; rights
::                  2. Proper firewall configuration to allow connection
::                  3. Proper permissions on the DHCP backup directory
:: Author:          vocatus
:: Version:         1.1b - Changed DATE to CUR_DATE format to be consistent with all other scripts
::                  1.1 - Comments work
::                      - Tuned some parameters (ping count on checking)
::                      - Some logging tweaks
::                      - Renamed FAILOVER_DELAY to FAILOVER_RECHECK_DELAY for clarity
::                  1.0d Some logging tweaks
::                  1.0c Some logging tweaks
::                  1.0 Initial write
:: Notes:           I wrote this script after failing to find a satisfactory method of performing
::                  watchdog/failover between two Windows Server 2008 R2 DHCP servers.
::                 
:: Use:             This script has two modes: &quot;Watchdog&quot; and &quot;Failover.&quot; 
::                  - Watchdog checks the status of the remote DHCP service, logs it, and then grabs the remote DHCP db backup file and imports it.
::                  - Failover mode is activated when the script cannot determine the status of the remote DHCP server. The script then activates 
::                    the local DHCP server with the latest backup copy it successfully retrieved from the primary server.
::                  
:: Instructions: 
::                  1. Tune the variables in this script to your desired backup location and frequency
::                  2. On the primary server: set the DHCP backup interval to your desired backup frequency. The value is in minutes; I recommend 5 minutes.
::                     You do this by modifying this registry key: HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Services\DHCPServer\Parameters\BackupInterval
::                  3. On the backup server:  set this script to run as a scheduled task. I recommend every 10 minutes. 
:: Notice: 
::                 !! Make sure to set it only to run if it isn't already running! If there is a failover you could have 
::                    Task Scheduler spawn a new instance of the script every n minutes and end up with hundreds of copies
::                    of this script running.


:: Prep
SETLOCAL
@echo off
cls
set VERSION=1.1b
title [DHCP Server Watchdog v%VERSION%]


:::::::::::::::
:: Variables :: - Set these. Do not use trailing slashes (\) in directory names (this is important!).
:::::::::::::::

:: Remote server is the PRIMARY DHCP server we're watching. Use a hostname or IP address.
set REMOTE_SERVER=mikado

:: Location of the DHCP backup on the primary server
:: Best practice is to leave this alone, unless you have a custom backup location.
:: The script builds the backup line like this: \\%REMOTE_SERVER%\c$\%REMOTE_BACKUP_PATH%
set REMOTE_BACKUP_PATH=Windows\system32\dhcp\backup

:: Location of your backup/standby file. I normally copy directly to my backup server's DHCP directory. 
:: The script builds the local backup line like this: c:\windows\system32\dhcp\[backup folders]
set LOCAL_BACKUP_PATH=%SystemRoot%\system32\dhcp

:: When a failover is triggered, how many seconds should we wait in between each attempt to contact the primary server again?
set FAILOVER_RECHECK_DELAY=15

:: Log options. Don't put an extension on the log file name. (Important!) The script sets this later on.
set LOGPATH=%SystemDrive%\Logs
set LOGFILE=%COMPUTERNAME%_DHCP_watchdog

:: Max log file size allowed (in bytes) before rotation and archive. I recommend setting this to 2 MB (2097152).
:: Example: 524288 is half a megabyte (~500KB)
set LOG_MAX_SIZE=2097152

:: \/ Don't touch anything below this line. If you do, you will break something.
set CUR_DATE=%DATE:~-4%-%DATE:~4,2%-%DATE:~7,2%


:::::::::::::::::::::::
:: LOG FILE HANDLING :: - This section handles the log file
:::::::::::::::::::::::

:: Make the logfile if it doesn't exist
if not exist %LOGPATH% mkdir %LOGPATH%
if not exist %LOGPATH%\%LOGFILE%.log goto new_log

:: Check log size. If it hasn't exceeded our size limit, jump straight to Watchdog mode
for %%R in (%LOGPATH%\%LOGFILE%.log) do if %%~zR LSS %LOG_MAX_SIZE% goto newrun

:: However, if the log was too big, go ahead and rotate it.
pushd %LOGPATH%
del %LOGFILE%.ancient 2&gt;NUL
rename %LOGFILE%.oldest %LOGFILE%.ancient 2&gt;NUL
rename %LOGFILE%.older %LOGFILE%.oldest 2&gt;NUL
rename %LOGFILE%.old %LOGFILE%.older 2&gt;NUL
rename %LOGFILE%.log %LOGFILE%.old 2&gt;NUL
popd

:: And then create the header for the new log file
:new_log
echo -------------------------------------------------------------------------------------&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo  Initializing new DHCP Server Watchdog log on %CUR_DATE% at %TIME%, max log size %LOG_MAX_SIZE% bytes&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo -------------------------------------------------------------------------------------&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1

:: New run section - if we just launched the script, write a header for this run
:newrun
echo -------------------------------------------------------------------------------------&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo  DHCP Server Watchdog v%VERSION%, %CUR_DATE%&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo   Running as %USERDOMAIN%\%USERNAME% on %COMPUTERNAME%&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo  Job Options&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo   Log location:            %LOGPATH%\%LOGFILE%.log&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo   Log max size:            %LOG_MAX_SIZE% bytes&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo   Watching primary server: %REMOTE_SERVER%&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo   Mirroring this DHCP db:  %REMOTE_BACKUP_PATH%&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo   Local backup location:   %LOCAL_BACKUP_PATH%&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo -------------------------------------------------------------------------------------&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Starting Watchdog mode.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo.
echo  DHCP Server Watchdog v%VERSION%
echo   Running as: %USERDOMAIN%\%USERNAME% on %COMPUTERNAME%
echo   Log:        %LOGPATH%\%LOGFILE%.log


:::::::::::::::::::
:: WATCHDOG MODE ::
:::::::::::::::::::

:watchdog

:: Ping the server to see if it's up
echo.
echo   Verifying proper operation of DHCP server on %REMOTE_SERVER%, please wait...
echo.
echo %TIME%         Pinging %REMOTE_SERVER%...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Pinging %REMOTE_SERVER%...
ping %REMOTE_SERVER% -n 2 &gt;NUL 2&gt;&amp;1
if %ERRORLEVEL%==1 echo %TIME% WARNING %REMOTE_SERVER% failed to respond to ping. &amp;&amp; echo %TIME% WARNING %REMOTE_SERVER% failed to respond to ping.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
if not %ERRORLEVEL%==1 echo %TIME% SUCCESS %REMOTE_SERVER% responded to ping. &amp;&amp; echo %TIME% SUCCESS %REMOTE_SERVER% responded to ping.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1

:: Check &amp; Log
echo %TIME%         Checking DHCP server status on %REMOTE_SERVER%...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Checking DHCP server status on %REMOTE_SERVER%...

:: Reset ERRORLEVEL back to 0
ver &gt; NUL

:: Use &quot;SC&quot; to check the status of &quot;Dhcpserver&quot; service, find the &quot;RUNNING&quot; state, and act accordingly based on the return code
sc \\%REMOTE_SERVER% query Dhcpserver | find &quot;RUNNING&quot; &gt;NUL 2&gt;&amp;1
if %ERRORLEVEL%==0 echo %TIME% SUCCESS The DHCP service is running on %REMOTE_SERVER%.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
if %ERRORLEVEL%==0 echo %TIME% SUCCESS The DHCP service is running on %REMOTE_SERVER%.

:: This section only executes if the test failed.
if not %ERRORLEVEL%==0 ( 
	echo %TIME% FAILURE The DHCP service is not running on %REMOTE_SERVER%.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME%         Activating failover procedure. Local DHCP server will be initialized using most recent successful backup.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME% FAILURE The DHCP service is not running on %REMOTE_SERVER%.
	echo %TIME%         Activating failover procedure. Local DHCP server will be initialized using most recent successful backup.
	goto failover
	)

:: Reset ERRORLEVEL back to 0
ver &gt; NUL

:: Fetch
echo %TIME%         Fetching DHCP database backup from %REMOTE_SERVER%...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Fetching DHCP database backup from %REMOTE_SERVER%...
xcopy \\%REMOTE_SERVER%\c$\%REMOTE_BACKUP_PATH%\* %LOCAL_BACKUP_PATH%\backup_new_pending\ /E /Y /Q &gt;NUL

:: If the copy SUCCEEDED, this executes
if %ERRORLEVEL%==0 ( 
	echo %TIME% SUCCESS Backup fetched from %REMOTE_SERVER%.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME% SUCCESS Backup fetched from %REMOTE_SERVER%.
	echo %TIME%         Rotating database backups...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME%         Rotating database backups...
	:: Rotate backups and use newest copy
	rmdir /S /Q %LOCAL_BACKUP_PATH%\backup5
	if exist %LOCAL_BACKUP_PATH%\backup4 move /Y %LOCAL_BACKUP_PATH%\backup4 %LOCAL_BACKUP_PATH%\backup5 2&gt;&amp;1
	if exist %LOCAL_BACKUP_PATH%\backup3 move /Y %LOCAL_BACKUP_PATH%\backup3 %LOCAL_BACKUP_PATH%\backup4 2&gt;&amp;1
	if exist %LOCAL_BACKUP_PATH%\backup2 move /Y %LOCAL_BACKUP_PATH%\backup2 %LOCAL_BACKUP_PATH%\backup3 2&gt;&amp;1
	if exist %LOCAL_BACKUP_PATH%\backup move /Y %LOCAL_BACKUP_PATH%\backup %LOCAL_BACKUP_PATH%\backup2 2&gt;&amp;1
	move /Y %LOCAL_BACKUP_PATH%\backup_new_pending %LOCAL_BACKUP_PATH%\backup &gt;NUL
	echo %TIME%         Database backups rotated.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME%         Database backups rotated.
	)

:: If the copy FAILED, this executes:
if not %ERRORLEVEL%==0 ( 
	echo %TIME% WARNING There was an error copying the backup from %REMOTE_SERVER%.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME%         You may want to look into this since we were able to check the DHCPserver service status but the file copy failed.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME%         Skipping new database import due to copy failure.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME%         Job complete with errors.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME% WARNING There was an error copying the backup from %REMOTE_SERVER%.
	echo %TIME%         You may want to look into this since we were able to check the DHCPserver service status but the file copy failed.
	echo %TIME%         Skipping new database import due to copy failure.
	echo %TIME%         Job complete with errors.
	)
	
:: Import database
ver &gt; NUL
echo %TIME%         Starting local DHCP server to import new database...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Starting local DHCP server to import new database...
	net start Dhcpserver 2&gt;&amp;1
echo %TIME%         Local DHCP server running. Performing import...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Local DHCP server running. Performing import...
	netsh dhcp server restore %LOCAL_BACKUP_PATH%\backup
echo %TIME%         Import complete.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Import complete.
echo %TIME%         Stopping local DHCP server...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Stopping local DHCP server...
	net stop Dhcpserver 2&gt;&amp;1
echo %TIME%         Local DHCP server stopped.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Local DHCP server stopped.
echo %TIME% SUCCESS Job complete, DHCP database backed up and ready for use. Exiting.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME% SUCCESS Job complete, DHCP database backed up and ready for use. Exiting.
goto EOF


:::::::::::::::::::
:: FAILOVER MODE ::
:::::::::::::::::::

:failover
:: Log this AND display to console
echo %TIME% WARNING Failover activated.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Starting local DHCP server using most recent successful backup...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo.
echo %TIME% WARNING Could not contact primary DHCP server &quot;%REMOTE_SERVER%.&quot; Failover activated.
echo %TIME%         Starting local DHCP server using most recent successful backup...
echo.
net start Dhcpserver
echo %TIME%         Local DHCP server started.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Entering monitoring loop. Checking if %REMOTE_SERVER% is back up every %FAILOVER_RECHECK_DELAY% seconds...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME%         Local DHCP server started.
echo %TIME%         Entering monitoring loop. Checking if %REMOTE_SERVER% is back up every %FAILOVER_RECHECK_DELAY% seconds...


:failover_loop
:: First we ping the server
ver &gt;NUL
ping %REMOTE_SERVER% -n 3 &gt;NUL 2&gt;&amp;1
:: If no ping response, this section executes
if %ERRORLEVEL%==1 (
	echo %TIME% FAILURE No ping response from %REMOTE_SERVER%. Waiting %FAILOVER_RECHECK_DELAY% seconds to check again.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME% FAILURE No ping response from %REMOTE_SERVER%. Waiting %FAILOVER_RECHECK_DELAY% seconds to check again.
	ping localhost -n %FAILOVER_RECHECK_DELAY% &gt;NUL 2&gt;&amp;1
	goto failover_loop
	)

:: If yes ping response, this section executes
:: This declaration is required to get the nested IF ERRORLEVEL test to function correctly
SETLOCAL ENABLEDELAYEDEXPANSION
if not %ERRORLEVEL%==1 (
	echo %TIME% NOTICE %REMOTE_SERVER% is responding to pings.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME% NOTICE %REMOTE_SERVER% is responding to pings.
	echo %TIME%        Checking DHCP server status on %REMOTE_SERVER%...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
	echo %TIME%        Checking DHCP server status on %REMOTE_SERVER%...
	
	:: This section checks to see if the Dhcpserver service is back up and acts accordingly
	sc \\%REMOTE_SERVER% query Dhcpserver | find &quot;RUNNING&quot; &gt;NUL 2&gt;&amp;1
		:: The exclamation points around ERRORLEVEL here prevent it from incorrectly being expanded using the external ERRORLEVEL results from the first IF statement
		if !ERRORLEVEL!==0 (
				echo %TIME% SUCCESS The DHCP service is running on %REMOTE_SERVER%.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
				echo %TIME% SUCCESS The DHCP service is running on %REMOTE_SERVER%.
				echo %TIME%         The primary DHCP server %REMOTE_SERVER% is back up. Stopping local DHCP service...&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
				echo %TIME%         The primary DHCP server %REMOTE_SERVER% is back up. Stopping local DHCP service...
				net stop Dhcpserver
				echo %TIME%         Recovery complete. Exiting.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
				echo %TIME%         Recovery complete. Exiting.
				goto EOF
				)
	)
ENDLOCAL

:: If the host responds to pings but the DHCP service isn't running, this executes
echo %TIME% FAILURE %REMOTE_SERVER% is responding to pings, but DHCP isn't responding (yet?). Will try again in %FAILOVER_RECHECK_DELAY% seconds.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
echo %TIME% FAILURE %REMOTE_SERVER% is responding to pings, but DHCP isn't responding (yet?). Will try again in %FAILOVER_RECHECK_DELAY% seconds.
ver &gt;NUL
goto failover_loop

ENDLOCAL
echo.&gt;&gt; %LOGPATH%\%LOGFILE%.log 2&gt;&amp;1
:EOF
</code></pre>

</div>

    <footer class="blog-footer">
        <p>&copy; Joel &quot;Jaykul&quot; Bennett 2018</p>
    </footer>

    <script src="https://code.jquery.com/jquery-3.2.1.slim.min.js" integrity="sha384-KJ3o2DKtIkvYIK3UENzmM7KCkRr/rE9/Qpg6aAZGJwFDMVNA/GpGFF93hXpG5KkN"
        crossorigin="anonymous"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/popper.js/1.12.9/umd/popper.min.js" integrity="sha384-ApNbgh9B+Y1QKtv3Rn7W3mgPxhU9K/ScQsAP7hUibX39j7fakFPskvXusvfa0b4Q"
        crossorigin="anonymous"></script>
    <script src="https://maxcdn.bootstrapcdn.com/bootstrap/4.0.0/js/bootstrap.min.js" integrity="sha384-JZR6Spejh4U02d8jOt6vLEHfe/JQGiRRSQQxSfFWpi1MquVdAyjUar5+76PVCmYl"
        crossorigin="anonymous"></script>
    <script src="/js/main.js"></script>

    <script>
      (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
      (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
      m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
      })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

      ga('create', 'UA-59988721-1', 'auto');
      ga('send', 'pageview');

      $(function () {
          $('#contentTabs a:first').tab('show')
      })

    </script>
</body>
</html>